///// Look-up tables for AES
///// Will be kept state constant memory for fast access.

// The S-box used state AES Encription
__constant unsigned char AES_SBox[256] = 
{
   0x63, 0x7C, 0x77, 0x7B, 0xF2, 0x6B, 0x6F, 0xC5, 0x30, 0x01, 0x67, 0x2B, 0xFE, 0xD7, 0xAB, 0x76,
   0xCA, 0x82, 0xC9, 0x7D, 0xFA, 0x59, 0x47, 0xF0, 0xAD, 0xD4, 0xA2, 0xAF, 0x9C, 0xA4, 0x72, 0xC0,
   0xB7, 0xFD, 0x93, 0x26, 0x36, 0x3F, 0xF7, 0xCC, 0x34, 0xA5, 0xE5, 0xF1, 0x71, 0xD8, 0x31, 0x15,
   0x04, 0xC7, 0x23, 0xC3, 0x18, 0x96, 0x05, 0x9A, 0x07, 0x12, 0x80, 0xE2, 0xEB, 0x27, 0xB2, 0x75,
   0x09, 0x83, 0x2C, 0x1A, 0x1B, 0x6E, 0x5A, 0xA0, 0x52, 0x3B, 0xD6, 0xB3, 0x29, 0xE3, 0x2F, 0x84,
   0x53, 0xD1, 0x00, 0xED, 0x20, 0xFC, 0xB1, 0x5B, 0x6A, 0xCB, 0xBE, 0x39, 0x4A, 0x4C, 0x58, 0xCF,
   0xD0, 0xEF, 0xAA, 0xFB, 0x43, 0x4D, 0x33, 0x85, 0x45, 0xF9, 0x02, 0x7F, 0x50, 0x3C, 0x9F, 0xA8,
   0x51, 0xA3, 0x40, 0x8F, 0x92, 0x9D, 0x38, 0xF5, 0xBC, 0xB6, 0xDA, 0x21, 0x10, 0xFF, 0xF3, 0xD2,
   0xCD, 0x0C, 0x13, 0xEC, 0x5F, 0x97, 0x44, 0x17, 0xC4, 0xA7, 0x7E, 0x3D, 0x64, 0x5D, 0x19, 0x73,
   0x60, 0x81, 0x4F, 0xDC, 0x22, 0x2A, 0x90, 0x88, 0x46, 0xEE, 0xB8, 0x14, 0xDE, 0x5E, 0x0B, 0xDB,
   0xE0, 0x32, 0x3A, 0x0A, 0x49, 0x06, 0x24, 0x5C, 0xC2, 0xD3, 0xAC, 0x62, 0x91, 0x95, 0xE4, 0x79,
   0xE7, 0xC8, 0x37, 0x6D, 0x8D, 0xD5, 0x4E, 0xA9, 0x6C, 0x56, 0xF4, 0xEA, 0x65, 0x7A, 0xAE, 0x08,
   0xBA, 0x78, 0x25, 0x2E, 0x1C, 0xA6, 0xB4, 0xC6, 0xE8, 0xDD, 0x74, 0x1F, 0x4B, 0xBD, 0x8B, 0x8A,
   0x70, 0x3E, 0xB5, 0x66, 0x48, 0x03, 0xF6, 0x0E, 0x61, 0x35, 0x57, 0xB9, 0x86, 0xC1, 0x1D, 0x9E,
   0xE1, 0xF8, 0x98, 0x11, 0x69, 0xD9, 0x8E, 0x94, 0x9B, 0x1E, 0x87, 0xE9, 0xCE, 0x55, 0x28, 0xDF,
   0x8C, 0xA1, 0x89, 0x0D, 0xBF, 0xE6, 0x42, 0x68, 0x41, 0x99, 0x2D, 0x0F, 0xB0, 0x54, 0xBB, 0x16
};

// The S-box used state AES Decription
__constant unsigned char AES_Inv_SBox[256] = 
{
   0x52, 0x09, 0x6A, 0xD5, 0x30, 0x36, 0xA5, 0x38, 0xBF, 0x40, 0xA3, 0x9E, 0x81, 0xF3, 0xD7, 0xFB,
   0x7C, 0xE3, 0x39, 0x82, 0x9B, 0x2F, 0xFF, 0x87, 0x34, 0x8E, 0x43, 0x44, 0xC4, 0xDE, 0xE9, 0xCB,
   0x54, 0x7B, 0x94, 0x32, 0xA6, 0xC2, 0x23, 0x3D, 0xEE, 0x4C, 0x95, 0x0B, 0x42, 0xFA, 0xC3, 0x4E,
   0x08, 0x2E, 0xA1, 0x66, 0x28, 0xD9, 0x24, 0xB2, 0x76, 0x5B, 0xA2, 0x49, 0x6D, 0x8B, 0xD1, 0x25,
   0x72, 0xF8, 0xF6, 0x64, 0x86, 0x68, 0x98, 0x16, 0xD4, 0xA4, 0x5C, 0xCC, 0x5D, 0x65, 0xB6, 0x92,
   0x6C, 0x70, 0x48, 0x50, 0xFD, 0xED, 0xB9, 0xDA, 0x5E, 0x15, 0x46, 0x57, 0xA7, 0x8D, 0x9D, 0x84,
   0x90, 0xD8, 0xAB, 0x00, 0x8C, 0xBC, 0xD3, 0x0A, 0xF7, 0xE4, 0x58, 0x05, 0xB8, 0xB3, 0x45, 0x06,
   0xD0, 0x2C, 0x1E, 0x8F, 0xCA, 0x3F, 0x0F, 0x02, 0xC1, 0xAF, 0xBD, 0x03, 0x01, 0x13, 0x8A, 0x6B,
   0x3A, 0x91, 0x11, 0x41, 0x4F, 0x67, 0xDC, 0xEA, 0x97, 0xF2, 0xCF, 0xCE, 0xF0, 0xB4, 0xE6, 0x73,
   0x96, 0xAC, 0x74, 0x22, 0xE7, 0xAD, 0x35, 0x85, 0xE2, 0xF9, 0x37, 0xE8, 0x1C, 0x75, 0xDF, 0x6E,
   0x47, 0xF1, 0x1A, 0x71, 0x1D, 0x29, 0xC5, 0x89, 0x6F, 0xB7, 0x62, 0x0E, 0xAA, 0x18, 0xBE, 0x1B,
   0xFC, 0x56, 0x3E, 0x4B, 0xC6, 0xD2, 0x79, 0x20, 0x9A, 0xDB, 0xC0, 0xFE, 0x78, 0xCD, 0x5A, 0xF4,
   0x1F, 0xDD, 0xA8, 0x33, 0x88, 0x07, 0xC7, 0x31, 0xB1, 0x12, 0x10, 0x59, 0x27, 0x80, 0xEC, 0x5F,
   0x60, 0x51, 0x7F, 0xA9, 0x19, 0xB5, 0x4A, 0x0D, 0x2D, 0xE5, 0x7A, 0x9F, 0x93, 0xC9, 0x9C, 0xEF,
   0xA0, 0xE0, 0x3B, 0x4D, 0xAE, 0x2A, 0xF5, 0xB0, 0xC8, 0xEB, 0xBB, 0x3C, 0x83, 0x53, 0x99, 0x61,
   0x17, 0x2B, 0x04, 0x7E, 0xBA, 0x77, 0xD6, 0x26, 0xE1, 0x69, 0x14, 0x63, 0x55, 0x21, 0x0C, 0x7D
};

// Galois-field pre-calculated values
__constant unsigned char AES_Galois_2[256] = 
{
	0x00, 0x02, 0x04, 0x06, 0x08, 0x0A, 0x0C, 0x0E, 0x10, 0x12, 0x14, 0x16, 0x18, 0x1A, 0x1C, 0x1E,
	0x20, 0x22, 0x24, 0x26, 0x28, 0x2A, 0x2C, 0x2E, 0x30, 0x32, 0x34, 0x36, 0x38, 0x3A, 0x3C, 0x3E,
	0x40, 0x42, 0x44, 0x46, 0x48, 0x4A, 0x4C, 0x4E, 0x50, 0x52, 0x54, 0x56, 0x58, 0x5A, 0x5C, 0x5E,
	0x60, 0x62, 0x64, 0x66, 0x68, 0x6A, 0x6C, 0x6E, 0x70, 0x72, 0x74, 0x76, 0x78, 0x7A, 0x7C, 0x7E,
	0x80, 0x82, 0x84, 0x86, 0x88, 0x8A, 0x8C, 0x8E, 0x90, 0x92, 0x94, 0x96, 0x98, 0x9A, 0x9C, 0x9E,
	0xA0, 0xA2, 0xA4, 0xA6, 0xA8, 0xAA, 0xAC, 0xAE, 0xB0, 0xB2, 0xB4, 0xB6, 0xB8, 0xBA, 0xBC, 0xBE,
	0xC0, 0xC2, 0xC4, 0xC6, 0xC8, 0xCA, 0xCC, 0xCE, 0xD0, 0xD2, 0xD4, 0xD6, 0xD8, 0xDA, 0xDC, 0xDE,
	0xE0, 0xE2, 0xE4, 0xE6, 0xE8, 0xEA, 0xEC, 0xEE, 0xF0, 0xF2, 0xF4, 0xF6, 0xF8, 0xFA, 0xFC, 0xFE,
	0x1B, 0x19, 0x1F, 0x1D, 0x13, 0x11, 0x17, 0x15, 0x0B, 0x09, 0x0F, 0x0D, 0x03, 0x01, 0x07, 0x05,
	0x3B, 0x39, 0x3F, 0x3D, 0x33, 0x31, 0x37, 0x35, 0x2B, 0x29, 0x2F, 0x2D, 0x23, 0x21, 0x27, 0x25,
	0x5B, 0x59, 0x5F, 0x5D, 0x53, 0x51, 0x57, 0x55, 0x4B, 0x49, 0x4F, 0x4D, 0x43, 0x41, 0x47, 0x45,
	0x7B, 0x79, 0x7F, 0x7D, 0x73, 0x71, 0x77, 0x75, 0x6B, 0x69, 0x6F, 0x6D, 0x63, 0x61, 0x67, 0x65,
	0x9B, 0x99, 0x9F, 0x9D, 0x93, 0x91, 0x97, 0x95, 0x8B, 0x89, 0x8F, 0x8D, 0x83, 0x81, 0x87, 0x85,
	0xBB, 0xB9, 0xBF, 0xBD, 0xB3, 0xB1, 0xB7, 0xB5, 0xAB, 0xA9, 0xAF, 0xAD, 0xA3, 0xA1, 0xA7, 0xA5,
	0xDB, 0xD9, 0xDF, 0xDD, 0xD3, 0xD1, 0xD7, 0xD5, 0xCB, 0xC9, 0xCF, 0xCD, 0xC3, 0xC1, 0xC7, 0xC5,
	0xFB, 0xF9, 0xFF, 0xFD, 0xF3, 0xF1, 0xF7, 0xF5, 0xEB, 0xE9, 0xEF, 0xED, 0xE3, 0xE1, 0xE7, 0xE5
};

__constant unsigned char AES_Galois_3[256] = 
{
	0x00, 0x03, 0x06, 0x05, 0x0C, 0x0F, 0x0A, 0x09, 0x18, 0x1B, 0x1E, 0x1D, 0x14, 0x17, 0x12, 0x11,
	0x30, 0x33, 0x36, 0x35, 0x3C, 0x3F, 0x3A, 0x39, 0x28, 0x2B, 0x2E, 0x2D, 0x24, 0x27, 0x22, 0x21,
	0x60, 0x63, 0x66, 0x65, 0x6C, 0x6F, 0x6A, 0x69, 0x78, 0x7B, 0x7E, 0x7D, 0x74, 0x77, 0x72, 0x71,
	0x50, 0x53, 0x56, 0x55, 0x5C, 0x5F, 0x5A, 0x59, 0x48, 0x4B, 0x4E, 0x4D, 0x44, 0x47, 0x42, 0x41,
	0xC0, 0xC3, 0xC6, 0xC5, 0xCC, 0xCF, 0xCA, 0xC9, 0xD8, 0xDB, 0xDE, 0xDD, 0xD4, 0xD7, 0xD2, 0xD1,
	0xF0, 0xF3, 0xF6, 0xF5, 0xFC, 0xFF, 0xFA, 0xF9, 0xE8, 0xEB, 0xEE, 0xED, 0xE4, 0xE7, 0xE2, 0xE1,
	0xA0, 0xA3, 0xA6, 0xA5, 0xAC, 0xAF, 0xAA, 0xA9, 0xB8, 0xBB, 0xBE, 0xBD, 0xB4, 0xB7, 0xB2, 0xB1,
	0x90, 0x93, 0x96, 0x95, 0x9C, 0x9F, 0x9A, 0x99, 0x88, 0x8B, 0x8E, 0x8D, 0x84, 0x87, 0x82, 0x81,
	0x9B, 0x98, 0x9D, 0x9E, 0x97, 0x94, 0x91, 0x92, 0x83, 0x80, 0x85, 0x86, 0x8F, 0x8C, 0x89, 0x8A,
	0xAB, 0xA8, 0xAD, 0xAE, 0xA7, 0xA4, 0xA1, 0xA2, 0xB3, 0xB0, 0xB5, 0xB6, 0xBF, 0xBC, 0xB9, 0xBA,
	0xFB, 0xF8, 0xFD, 0xFE, 0xF7, 0xF4, 0xF1, 0xF2, 0xE3, 0xE0, 0xE5, 0xE6, 0xEF, 0xEC, 0xE9, 0xEA,
	0xCB, 0xC8, 0xCD, 0xCE, 0xC7, 0xC4, 0xC1, 0xC2, 0xD3, 0xD0, 0xD5, 0xD6, 0xDF, 0xDC, 0xD9, 0xDA,
	0x5B, 0x58, 0x5D, 0x5E, 0x57, 0x54, 0x51, 0x52, 0x43, 0x40, 0x45, 0x46, 0x4F, 0x4C, 0x49, 0x4A,
	0x6B, 0x68, 0x6D, 0x6E, 0x67, 0x64, 0x61, 0x62, 0x73, 0x70, 0x75, 0x76, 0x7F, 0x7C, 0x79, 0x7A,
	0x3B, 0x38, 0x3D, 0x3E, 0x37, 0x34, 0x31, 0x32, 0x23, 0x20, 0x25, 0x26, 0x2F, 0x2C, 0x29, 0x2A,
	0x0B, 0x08, 0x0D, 0x0E, 0x07, 0x04, 0x01, 0x02, 0x13, 0x10, 0x15, 0x16, 0x1F, 0x1C, 0x19, 0x1A
};

__constant unsigned char AES_Galois_9[256] = 
{
	0x00, 0x09, 0x12, 0x1B, 0x24, 0x2D, 0x36, 0x3F, 0x48, 0x41, 0x5A, 0x53, 0x6C, 0x65, 0x7E, 0x77,
	0x90, 0x99, 0x82, 0x8B, 0xB4, 0xBD, 0xA6, 0xAF, 0xD8, 0xD1, 0xCA, 0xC3, 0xFC, 0xF5, 0xEE, 0xE7,
	0x3B, 0x32, 0x29, 0x20, 0x1F, 0x16, 0x0D, 0x04, 0x73, 0x7A, 0x61, 0x68, 0x57, 0x5E, 0x45, 0x4C,
	0xAB, 0xA2, 0xB9, 0xB0, 0x8F, 0x86, 0x9D, 0x94, 0xE3, 0xEA, 0xF1, 0xF8, 0xC7, 0xCE, 0xD5, 0xDC,
	0x76, 0x7F, 0x64, 0x6D, 0x52, 0x5B, 0x40, 0x49, 0x3E, 0x37, 0x2C, 0x25, 0x1A, 0x13, 0x08, 0x01,
	0xE6, 0xEF, 0xF4, 0xFD, 0xC2, 0xCB, 0xD0, 0xD9, 0xAE, 0xA7, 0xBC, 0xB5, 0x8A, 0x83, 0x98, 0x91,
	0x4D, 0x44, 0x5F, 0x56, 0x69, 0x60, 0x7B, 0x72, 0x05, 0x0C, 0x17, 0x1E, 0x21, 0x28, 0x33, 0x3A,
	0xDD, 0xD4, 0xCF, 0xC6, 0xF9, 0xF0, 0xEB, 0xE2, 0x95, 0x9C, 0x87, 0x8E, 0xB1, 0xB8, 0xA3, 0xAA,
	0xEC, 0xE5, 0xFE, 0xF7, 0xC8, 0xC1, 0xDA, 0xD3, 0xA4, 0xAD, 0xB6, 0xBF, 0x80, 0x89, 0x92, 0x9B,
	0x7C, 0x75, 0x6E, 0x67, 0x58, 0x51, 0x4A, 0x43, 0x34, 0x3D, 0x26, 0x2F, 0x10, 0x19, 0x02, 0x0B,
	0xD7, 0xDE, 0xC5, 0xCC, 0xF3, 0xFA, 0xE1, 0xE8, 0x9F, 0x96, 0x8D, 0x84, 0xBB, 0xB2, 0xA9, 0xA0,
	0x47, 0x4E, 0x55, 0x5C, 0x63, 0x6A, 0x71, 0x78, 0x0F, 0x06, 0x1D, 0x14, 0x2B, 0x22, 0x39, 0x30,
	0x9A, 0x93, 0x88, 0x81, 0xBE, 0xB7, 0xAC, 0xA5, 0xD2, 0xDB, 0xC0, 0xC9, 0xF6, 0xFF, 0xE4, 0xED,
	0x0A, 0x03, 0x18, 0x11, 0x2E, 0x27, 0x3C, 0x35, 0x42, 0x4B, 0x50, 0x59, 0x66, 0x6F, 0x74, 0x7D,
	0xA1, 0xA8, 0xB3, 0xBA, 0x85, 0x8C, 0x97, 0x9E, 0xE9, 0xE0, 0xFB, 0xF2, 0xCD, 0xC4, 0xDF, 0xD6,
	0x31, 0x38, 0x23, 0x2A, 0x15, 0x1C, 0x07, 0x0E, 0x79, 0x70, 0x6B, 0x62, 0x5D, 0x54, 0x4F, 0x46
};

__constant unsigned char AES_Galois_B[256] = 
{
	0x00, 0x0B, 0x16, 0x1D, 0x2C, 0x27, 0x3A, 0x31, 0x58, 0x53, 0x4E, 0x45, 0x74, 0x7F, 0x62, 0x69,
	0xB0, 0xBB, 0xA6, 0xAD, 0x9C, 0x97, 0x8A, 0x81, 0xE8, 0xE3, 0xFE, 0xF5, 0xC4, 0xCF, 0xD2, 0xD9,
	0x7B, 0x70, 0x6D, 0x66, 0x57, 0x5C, 0x41, 0x4A, 0x23, 0x28, 0x35, 0x3E, 0x0F, 0x04, 0x19, 0x12,
	0xCB, 0xC0, 0xDD, 0xD6, 0xE7, 0xEC, 0xF1, 0xFA, 0x93, 0x98, 0x85, 0x8E, 0xBF, 0xB4, 0xA9, 0xA2,
	0xF6, 0xFD, 0xE0, 0xEB, 0xDA, 0xD1, 0xCC, 0xC7, 0xAE, 0xA5, 0xB8, 0xB3, 0x82, 0x89, 0x94, 0x9F,
	0x46, 0x4D, 0x50, 0x5B, 0x6A, 0x61, 0x7C, 0x77, 0x1E, 0x15, 0x08, 0x03, 0x32, 0x39, 0x24, 0x2F,
	0x8D, 0x86, 0x9B, 0x90, 0xA1, 0xAA, 0xB7, 0xBC, 0xD5, 0xDE, 0xC3, 0xC8, 0xF9, 0xF2, 0xEF, 0xE4,
	0x3D, 0x36, 0x2B, 0x20, 0x11, 0x1A, 0x07, 0x0C, 0x65, 0x6E, 0x73, 0x78, 0x49, 0x42, 0x5F, 0x54,
	0xF7, 0xFC, 0xE1, 0xEA, 0xDB, 0xD0, 0xCD, 0xC6, 0xAF, 0xA4, 0xB9, 0xB2, 0x83, 0x88, 0x95, 0x9E,
	0x47, 0x4C, 0x51, 0x5A, 0x6B, 0x60, 0x7D, 0x76, 0x1F, 0x14, 0x09, 0x02, 0x33, 0x38, 0x25, 0x2E,
	0x8C, 0x87, 0x9A, 0x91, 0xA0, 0xAB, 0xB6, 0xBD, 0xD4, 0xDF, 0xC2, 0xC9, 0xF8, 0xF3, 0xEE, 0xE5,
	0x3C, 0x37, 0x2A, 0x21, 0x10, 0x1B, 0x06, 0x0D, 0x64, 0x6F, 0x72, 0x79, 0x48, 0x43, 0x5E, 0x55,
	0x01, 0x0A, 0x17, 0x1C, 0x2D, 0x26, 0x3B, 0x30, 0x59, 0x52, 0x4F, 0x44, 0x75, 0x7E, 0x63, 0x68,
	0xB1, 0xBA, 0xA7, 0xAC, 0x9D, 0x96, 0x8B, 0x80, 0xE9, 0xE2, 0xFF, 0xF4, 0xC5, 0xCE, 0xD3, 0xD8,
	0x7A, 0x71, 0x6C, 0x67, 0x56, 0x5D, 0x40, 0x4B, 0x22, 0x29, 0x34, 0x3F, 0x0E, 0x05, 0x18, 0x13,
	0xCA, 0xC1, 0xDC, 0xD7, 0xE6, 0xED, 0xF0, 0xFB, 0x92, 0x99, 0x84, 0x8F, 0xBE, 0xB5, 0xA8, 0xA3
};

__constant unsigned char AES_Galois_D[256] = 
{
	0x00, 0x0D, 0x1A, 0x17, 0x34, 0x39, 0x2E, 0x23, 0x68, 0x65, 0x72, 0x7F, 0x5C, 0x51, 0x46, 0x4B,
	0xD0, 0xDD, 0xCA, 0xC7, 0xE4, 0xE9, 0xFE, 0xF3, 0xB8, 0xB5, 0xA2, 0xAF, 0x8C, 0x81, 0x96, 0x9B,
	0xBB, 0xB6, 0xA1, 0xAC, 0x8F, 0x82, 0x95, 0x98, 0xD3, 0xDE, 0xC9, 0xC4, 0xE7, 0xEA, 0xFD, 0xF0,
	0x6B, 0x66, 0x71, 0x7C, 0x5F, 0x52, 0x45, 0x48, 0x03, 0x0E, 0x19, 0x14, 0x37, 0x3A, 0x2D, 0x20,
	0x6D, 0x60, 0x77, 0x7A, 0x59, 0x54, 0x43, 0x4E, 0x05, 0x08, 0x1F, 0x12, 0x31, 0x3C, 0x2B, 0x26,
	0xBD, 0xB0, 0xA7, 0xAA, 0x89, 0x84, 0x93, 0x9E, 0xD5, 0xD8, 0xCF, 0xC2, 0xE1, 0xEC, 0xFB, 0xF6,
	0xD6, 0xDB, 0xCC, 0xC1, 0xE2, 0xEF, 0xF8, 0xF5, 0xBE, 0xB3, 0xA4, 0xA9, 0x8A, 0x87, 0x90, 0x9D,
	0x06, 0x0B, 0x1C, 0x11, 0x32, 0x3F, 0x28, 0x25, 0x6E, 0x63, 0x74, 0x79, 0x5A, 0x57, 0x40, 0x4D,
	0xDA, 0xD7, 0xC0, 0xCD, 0xEE, 0xE3, 0xF4, 0xF9, 0xB2, 0xBF, 0xA8, 0xA5, 0x86, 0x8B, 0x9C, 0x91,
	0x0A, 0x07, 0x10, 0x1D, 0x3E, 0x33, 0x24, 0x29, 0x62, 0x6F, 0x78, 0x75, 0x56, 0x5B, 0x4C, 0x41,
	0x61, 0x6C, 0x7B, 0x76, 0x55, 0x58, 0x4F, 0x42, 0x09, 0x04, 0x13, 0x1E, 0x3D, 0x30, 0x27, 0x2A,
	0xB1, 0xBC, 0xAB, 0xA6, 0x85, 0x88, 0x9F, 0x92, 0xD9, 0xD4, 0xC3, 0xCE, 0xED, 0xE0, 0xF7, 0xFA,
	0xB7, 0xBA, 0xAD, 0xA0, 0x83, 0x8E, 0x99, 0x94, 0xDF, 0xD2, 0xC5, 0xC8, 0xEB, 0xE6, 0xF1, 0xFC,
	0x67, 0x6A, 0x7D, 0x70, 0x53, 0x5E, 0x49, 0x44, 0x0F, 0x02, 0x15, 0x18, 0x3B, 0x36, 0x21, 0x2C,
	0x0C, 0x01, 0x16, 0x1B, 0x38, 0x35, 0x22, 0x2F, 0x64, 0x69, 0x7E, 0x73, 0x50, 0x5D, 0x4A, 0x47,
	0xDC, 0xD1, 0xC6, 0xCB, 0xE8, 0xE5, 0xF2, 0xFF, 0xB4, 0xB9, 0xAE, 0xA3, 0x80, 0x8D, 0x9A, 0x97
};

__constant unsigned char AES_Galois_E[256] = 
{
	0x00, 0x0E, 0x1C, 0x12, 0x38, 0x36, 0x24, 0x2A, 0x70, 0x7E, 0x6C, 0x62, 0x48, 0x46, 0x54, 0x5A,
	0xE0, 0xEE, 0xFC, 0xF2, 0xD8, 0xD6, 0xC4, 0xCA, 0x90, 0x9E, 0x8C, 0x82, 0xA8, 0xA6, 0xB4, 0xBA,
	0xDB, 0xD5, 0xC7, 0xC9, 0xE3, 0xED, 0xFF, 0xF1, 0xAB, 0xA5, 0xB7, 0xB9, 0x93, 0x9D, 0x8F, 0x81,
	0x3B, 0x35, 0x27, 0x29, 0x03, 0x0D, 0x1F, 0x11, 0x4B, 0x45, 0x57, 0x59, 0x73, 0x7D, 0x6F, 0x61,
	0xAD, 0xA3, 0xB1, 0xBF, 0x95, 0x9B, 0x89, 0x87, 0xDD, 0xD3, 0xC1, 0xCF, 0xE5, 0xEB, 0xF9, 0xF7,
	0x4D, 0x43, 0x51, 0x5F, 0x75, 0x7B, 0x69, 0x67, 0x3D, 0x33, 0x21, 0x2F, 0x05, 0x0B, 0x19, 0x17,
	0x76, 0x78, 0x6A, 0x64, 0x4E, 0x40, 0x52, 0x5C, 0x06, 0x08, 0x1A, 0x14, 0x3E, 0x30, 0x22, 0x2C,
	0x96, 0x98, 0x8A, 0x84, 0xAE, 0xA0, 0xB2, 0xBC, 0xE6, 0xE8, 0xFA, 0xF4, 0xDE, 0xD0, 0xC2, 0xCC,
	0x41, 0x4F, 0x5D, 0x53, 0x79, 0x77, 0x65, 0x6B, 0x31, 0x3F, 0x2D, 0x23, 0x09, 0x07, 0x15, 0x1B,
	0xA1, 0xAF, 0xBD, 0xB3, 0x99, 0x97, 0x85, 0x8B, 0xD1, 0xDF, 0xCD, 0xC3, 0xE9, 0xE7, 0xF5, 0xFB,
	0x9A, 0x94, 0x86, 0x88, 0xA2, 0xAC, 0xBE, 0xB0, 0xEA, 0xE4, 0xF6, 0xF8, 0xD2, 0xDC, 0xCE, 0xC0,
	0x7A, 0x74, 0x66, 0x68, 0x42, 0x4C, 0x5E, 0x50, 0x0A, 0x04, 0x16, 0x18, 0x32, 0x3C, 0x2E, 0x20,
	0xEC, 0xE2, 0xF0, 0xFE, 0xD4, 0xDA, 0xC8, 0xC6, 0x9C, 0x92, 0x80, 0x8E, 0xA4, 0xAA, 0xB8, 0xB6,
	0x0C, 0x02, 0x10, 0x1E, 0x34, 0x3A, 0x28, 0x26, 0x7C, 0x72, 0x60, 0x6E, 0x44, 0x4A, 0x58, 0x56,
	0x37, 0x39, 0x2B, 0x25, 0x0F, 0x01, 0x13, 0x1D, 0x47, 0x49, 0x5B, 0x55, 0x7F, 0x71, 0x63, 0x6D,
	0xD7, 0xD9, 0xCB, 0xC5, 0xEF, 0xE1, 0xF3, 0xFD, 0xA7, 0xA9, 0xBB, 0xB5, 0x9F, 0x91, 0x83, 0x8D
};

//// Inline functions use for each step of the encription

// The block size used by AES is 128 bits, aka 16 bytes
// AES operates on a 4×4 column-major order matrix of bytes, termed the state.
/*
		state.s0, state.s4, state.s8, state.sC,
		state.s1, state.s5, state.s9, state.sD,
		state.s2, state.s6, state.sA, state.sE,
		state.s3, state.s7, state.sB, state.sF
*/	

inline uchar16 SubBytes(uchar16 state)
{
	return (uchar16)(
		AES_SBox[state.s0],
		AES_SBox[state.s1],
		AES_SBox[state.s2],
		AES_SBox[state.s3],
		AES_SBox[state.s4],
		AES_SBox[state.s5],
		AES_SBox[state.s6],
		AES_SBox[state.s7],
		AES_SBox[state.s8],
		AES_SBox[state.s9],
		AES_SBox[state.sA],
		AES_SBox[state.sB],
		AES_SBox[state.sC],
		AES_SBox[state.sD],
		AES_SBox[state.sE],
		AES_SBox[state.sF]		
		);
}

inline uchar16 InverseSubBytes(uchar16 state)
{
	return (uchar16)(
		AES_Inv_SBox[state.s0],
		AES_Inv_SBox[state.s1],
		AES_Inv_SBox[state.s2],
		AES_Inv_SBox[state.s3],
		AES_Inv_SBox[state.s4],
		AES_Inv_SBox[state.s5],
		AES_Inv_SBox[state.s6],
		AES_Inv_SBox[state.s7],
		AES_Inv_SBox[state.s8],
		AES_Inv_SBox[state.s9],
		AES_Inv_SBox[state.sA],
		AES_Inv_SBox[state.sB],
		AES_Inv_SBox[state.sC],
		AES_Inv_SBox[state.sD],
		AES_Inv_SBox[state.sE],
		AES_Inv_SBox[state.sF]		
		);
}

inline uchar16 ShiftRows(uchar16 state)
{
	return	state.s05AF49E38D27C16B;
}

inline uchar16 InverseShiftRows(uchar16 state)
{
	return	state.s0DA741EB852FC963;
}


// To reduce calculations and to completly avoid branches we used a 
// look-up table to calculate the multiplaction state the Galois field
inline uchar4 MixColumn (uchar4 state)
{
	return (uchar4)(
		AES_Galois_2[state.s0] ^ AES_Galois_3[state.s1] ^			   state.s2  ^				state.s3 ,	// 2 * state.s0 + 3 * state.s1 +     state.s2 +     state.s3
					 state.s0  ^ AES_Galois_2[state.s1] ^ AES_Galois_3[state.s2] ^				state.s3 ,	//     state.s0 + 2 * state.s1 + 3 * state.s2 +     state.s3
					 state.s0  ^			  state.s1  ^ AES_Galois_2[state.s2] ^ AES_Galois_3[state.s3],	//     state.s0 +     state.s1 + 2 * state.s2 + 3 * state.s3
		AES_Galois_3[state.s0] ^			  state.s1  ^			   state.s2  ^ AES_Galois_2[state.s3]	// 3 * state.s0 +     state.s1 +     state.s2 + 2 * state.s3
		);
}

inline uchar4 InverseMixColumn (uchar4 state)
{
	return (uchar4)(
		AES_Galois_E[state.s0] ^ AES_Galois_B[state.s1] ^ AES_Galois_D[state.s2] ^ AES_Galois_9[state.s3],
		AES_Galois_9[state.s0] ^ AES_Galois_E[state.s1] ^ AES_Galois_B[state.s2] ^ AES_Galois_D[state.s3],
		AES_Galois_D[state.s0] ^ AES_Galois_9[state.s1] ^ AES_Galois_E[state.s2] ^ AES_Galois_B[state.s3],
		AES_Galois_B[state.s0] ^ AES_Galois_D[state.s1] ^ AES_Galois_9[state.s2] ^ AES_Galois_E[state.s3]
		);
}


inline uchar16 MixColumns(uchar16 state)
{
	return (uchar16)(
		MixColumn(state.s0123),			// Each column is multiplied by a known matrix.
		MixColumn(state.s4567),			// Each column is multiplied by a known matrix.
		MixColumn(state.s89AB),			// Each column is multiplied by a known matrix.
		MixColumn(state.sCDEF)			// Each column is multiplied by a known matrix.
		);
}

inline uchar16 InverseMixColumns(uchar16 state)
{
	return (uchar16)(
		InverseMixColumn(state.s0123),			// Each column is multiplied by a known matrix.
		InverseMixColumn(state.s4567),			// Each column is multiplied by a known matrix.
		InverseMixColumn(state.s89AB),			// Each column is multiplied by a known matrix.
		InverseMixColumn(state.sCDEF)			// Each column is multiplied by a known matrix.
		);
}

inline uchar16 AddRoundKey(uchar16 state, uchar16 key)
{
	return state ^ key;
}


// OpenCL Kernel Entry Points

// AES ECB Version 1
// * Each kernel computes it's own block
// * Data should be pre-paded
__kernel void AES_ECB_Encrypt(
	__global uchar16* in, __global uchar16* out, 
	__global uchar16* keys, int rounds, int n)
{
	int idx = get_global_id(0);
	if (idx < n)
	{
		uchar16 state = in[idx];
	
		state = AddRoundKey(state, keys[0]);
	
		for (int i = 1; i < rounds-1; i++)
		{
			state = SubBytes(state);
			state = ShiftRows(state);
			state = MixColumns(state);
			state = AddRoundKey(state, keys[i]);
		}
	
		state = SubBytes(state);
		state = ShiftRows(state);
		state = AddRoundKey(state, keys[rounds - 1]);
		
		out[idx] = state;
	}
}
__kernel void AES_ECB_Decrypt(
	__global uchar16* in, __global uchar16* out, 
	__global uchar16* keys, int rounds, int n)
{
	int idx = get_global_id(0);
	if (idx < n)
	{
		uchar16 state = in[idx];
	
		state = AddRoundKey(state, keys[rounds - 1]);	
		state = InverseShiftRows(state);	
		state = InverseSubBytes(state);
	
		for (int i = rounds-2; i >= 1; i--)
		{
			state = AddRoundKey(state, keys[i]);
			state = InverseMixColumns(state);
			state = InverseShiftRows(state);
			state = InverseSubBytes(state);
		}
	
		state = AddRoundKey(state, keys[0]);

		out[idx] = state;
	}
}

// AES ECB Version 2
// * Each kernel computes it's own block
// * Data should be pre-paded
// * Used the data type int4 and removed fucntions
__kernel void AES_ECB_Encrypt2(
	__global uchar4* in, __global uchar4* out, 
	__global uchar4* keys, int rounds, int n)
{
	int idx = get_global_id(0);
	
	uchar4 state0,   state1,   state2,   state3;
	uchar4 state0_tmp, state1_tmp, state2_tmp, state3_tmp;
	
	if (idx < n)
	{
		state0 = in[idx*4]   ^ keys[0];
		state1 = in[idx*4+1] ^ keys[1];
		state2 = in[idx*4+2] ^ keys[2];
		state3 = in[idx*4+3] ^ keys[3];
	
		for (int i = 1; i < rounds-1; i++)
		{
			// state = SubBytes(state);
			// state = ShiftRows(state);
			state0_tmp.x = AES_SBox[state0.x]; state0_tmp.y = AES_SBox[state1.y]; state0_tmp.z = AES_SBox[state2.z]; state0_tmp.w = AES_SBox[state3.w]; 
			state1_tmp.x = AES_SBox[state1.x]; state1_tmp.y = AES_SBox[state2.y]; state1_tmp.z = AES_SBox[state3.z]; state1_tmp.w = AES_SBox[state0.w]; 
			state2_tmp.x = AES_SBox[state2.x]; state2_tmp.y = AES_SBox[state3.y]; state2_tmp.z = AES_SBox[state0.z]; state2_tmp.w = AES_SBox[state1.w]; 
			state3_tmp.x = AES_SBox[state3.x]; state3_tmp.y = AES_SBox[state0.y]; state3_tmp.z = AES_SBox[state1.z]; state3_tmp.w = AES_SBox[state2.w];
				
			// state = MixColumns(state);
			state0.x = AES_Galois_2[state0_tmp.x] ^ AES_Galois_3[state0_tmp.y] ^			  state0_tmp.z  ^			     state0_tmp.w ;	
			state0.y =			    state0_tmp.x  ^ AES_Galois_2[state0_tmp.y] ^ AES_Galois_3[state0_tmp.z] ^			     state0_tmp.w ;
			state0.z = 			    state0_tmp.x  ^	  		   state0_tmp.y  ^ AES_Galois_2[state0_tmp.z] ^ AES_Galois_3[state0_tmp.w];	
			state0.w = AES_Galois_3[state0_tmp.x] ^			   state0_tmp.y  ^			  state0_tmp.z  ^ AES_Galois_2[state0_tmp.w];	
			
			state1.x = AES_Galois_2[state1_tmp.x] ^ AES_Galois_3[state1_tmp.y] ^			  state1_tmp.z  ^			     state1_tmp.w ;	
			state1.y =			    state1_tmp.x  ^ AES_Galois_2[state1_tmp.y] ^ AES_Galois_3[state1_tmp.z] ^			     state1_tmp.w ;	
			state1.z = 			    state1_tmp.x  ^			   state1_tmp.y  ^ AES_Galois_2[state1_tmp.z] ^ AES_Galois_3[state1_tmp.w];	
			state1.w = AES_Galois_3[state1_tmp.x] ^			   state1_tmp.y  ^			  state1_tmp.z  ^ AES_Galois_2[state1_tmp.w];	
			
			state2.x = AES_Galois_2[state2_tmp.x] ^ AES_Galois_3[state2_tmp.y] ^			  state2_tmp.z  ^			     state2_tmp.w ;	
			state2.y =			    state2_tmp.x  ^ AES_Galois_2[state2_tmp.y] ^ AES_Galois_3[state2_tmp.z] ^			     state2_tmp.w ;	
			state2.z = 			    state2_tmp.x  ^			   state2_tmp.y  ^ AES_Galois_2[state2_tmp.z] ^ AES_Galois_3[state2_tmp.w];	
			state2.w = AES_Galois_3[state2_tmp.x] ^			   state2_tmp.y  ^			  state2_tmp.z  ^ AES_Galois_2[state2_tmp.w];	
			
			state3.x = AES_Galois_2[state3_tmp.x] ^ AES_Galois_3[state3_tmp.y] ^			  state3_tmp.z  ^			     state3_tmp.w ;	
			state3.y =			    state3_tmp.x  ^ AES_Galois_2[state3_tmp.y] ^ AES_Galois_3[state3_tmp.z] ^			     state3_tmp.w ;	
			state3.z = 			    state3_tmp.x  ^			   state3_tmp.y  ^ AES_Galois_2[state3_tmp.z] ^ AES_Galois_3[state3_tmp.w];	
			state3.w = AES_Galois_3[state3_tmp.x] ^			   state3_tmp.y  ^			  state3_tmp.z  ^ AES_Galois_2[state3_tmp.w];	
			
			//state = AddRoundKey(state, keys[i]);
			state0 ^= keys[i*4];
			state1 ^= keys[i*4+1];
			state2 ^= keys[i*4+2];
			state3 ^= keys[i*4+3];
		}
	
		// state = SubBytes(state);
		state0.x = AES_SBox[state0.x]; state0.y = AES_SBox[state0.y]; state0.z = AES_SBox[state0.z]; state0.w = AES_SBox[state0.w];
		state1.x = AES_SBox[state1.x]; state1.y = AES_SBox[state1.y]; state1.z = AES_SBox[state1.z]; state1.w = AES_SBox[state1.w];
		state2.x = AES_SBox[state2.x]; state2.y = AES_SBox[state2.y]; state2.z = AES_SBox[state2.z]; state2.w = AES_SBox[state2.w];
		state3.x = AES_SBox[state3.x]; state3.y = AES_SBox[state3.y]; state3.z = AES_SBox[state3.z]; state3.w = AES_SBox[state3.w];
		
		//state = ShiftRows(state);
		state0_tmp = state0; state1_tmp = state1; state2_tmp = state2; state3_tmp = state3;
		state0.x = state0_tmp.x; state0.y = state1_tmp.y; state0.z = state2_tmp.z; state0.w = state3_tmp.w; 
		state1.x = state1_tmp.x; state1.y = state2_tmp.y; state1.z = state3_tmp.z; state1.w = state0_tmp.w; 
		state2.x = state2_tmp.x; state2.y = state3_tmp.y; state2.z = state0_tmp.z; state2.w = state1_tmp.w; 
		state3.x = state3_tmp.x; state3.y = state0_tmp.y; state3.z = state1_tmp.z; state3.w = state2_tmp.w;
			
		//state = AddRoundKey(state, keys[rounds - 1]);
		state0 ^= keys[(rounds-1)*4];
		state1 ^= keys[(rounds-1)*4+1];
		state2 ^= keys[(rounds-1)*4+2];
		state3 ^= keys[(rounds-1)*4+3];

		out[idx*4]   = state0;
		out[idx*4+1] = state1;
		out[idx*4+2] = state2;
		out[idx*4+3] = state3;
		
		//idx += get_num_groups(0) * get_local_size(0);
	}
}

// AES ECB Version 4
// * Each kernel computes it's own block
// * Data should be pre-paded
// * Used the data type int4 and removed fucntions
// * Local memory for tables
__kernel 
	__attribute__((reqd_work_group_size(256, 1, 1)))
	void AES_ECB_Encrypt3(
	__global uchar4* in, __global uchar4* out, 
	__global uchar4* keys, int rounds, int n)
{
	int idx   = get_global_id(0);
	int l_idx = get_local_id(0);
	
	uchar4 state0,     state1,     state2,     state3;
	uchar4 state0_tmp, state1_tmp, state2_tmp, state3_tmp;

	__local uchar sbox[256];
	__local uchar gal2[256];
	__local uchar gal3[256];

	// Cache frequently used data
	sbox[l_idx] = AES_SBox    [l_idx];
	gal2[l_idx] = AES_Galois_2[l_idx];
	gal3[l_idx] = AES_Galois_3[l_idx];
	
	barrier(CLK_LOCAL_MEM_FENCE);
		
	if (idx <= n)
	{
		// Read input from memory
		//state = AddRoundKey(state, keys[0]);
		state0 = in[idx*4]   ^ keys[0];
		state1 = in[idx*4+1] ^ keys[1];
		state2 = in[idx*4+2] ^ keys[2];
		state3 = in[idx*4+3] ^ keys[3];
			
		for (int i = 1; i < rounds-1; i++)
		{
			// state = SubBytes(state);
			// state = ShiftRows(state);
			state0_tmp.x = sbox[state0.x]; state0_tmp.y = sbox[state1.y]; state0_tmp.z = sbox[state2.z]; state0_tmp.w = sbox[state3.w]; 
			state1_tmp.x = sbox[state1.x]; state1_tmp.y = sbox[state2.y]; state1_tmp.z = sbox[state3.z]; state1_tmp.w = sbox[state0.w]; 
			state2_tmp.x = sbox[state2.x]; state2_tmp.y = sbox[state3.y]; state2_tmp.z = sbox[state0.z]; state2_tmp.w = sbox[state1.w]; 
			state3_tmp.x = sbox[state3.x]; state3_tmp.y = sbox[state0.y]; state3_tmp.z = sbox[state1.z]; state3_tmp.w = sbox[state2.w];

			// state = MixColumns(state);
			state0.x = gal2[state0_tmp.x] ^ gal3[state0_tmp.y] ^	  state0_tmp.z  ^      state0_tmp.w ;	
			state0.y =	    state0_tmp.x  ^ gal2[state0_tmp.y] ^ gal3[state0_tmp.z] ^      state0_tmp.w ;
			state0.z = 	    state0_tmp.x  ^      state0_tmp.y  ^ gal2[state0_tmp.z] ^ gal3[state0_tmp.w];	
			state0.w = gal3[state0_tmp.x] ^	     state0_tmp.y  ^	  state0_tmp.z  ^ gal2[state0_tmp.w];	
			
			state1.x = gal2[state1_tmp.x] ^ gal3[state1_tmp.y] ^	  state1_tmp.z  ^      state1_tmp.w ;	
			state1.y =	    state1_tmp.x  ^ gal2[state1_tmp.y] ^ gal3[state1_tmp.z] ^	   state1_tmp.w ;	
			state1.z = 	    state1_tmp.x  ^	     state1_tmp.y  ^ gal2[state1_tmp.z] ^ gal3[state1_tmp.w];	
			state1.w = gal3[state1_tmp.x] ^	     state1_tmp.y  ^	  state1_tmp.z  ^ gal2[state1_tmp.w];	
			
			state2.x = gal2[state2_tmp.x] ^ gal3[state2_tmp.y] ^	  state2_tmp.z  ^      state2_tmp.w ;	
			state2.y =	    state2_tmp.x  ^ gal2[state2_tmp.y] ^ gal3[state2_tmp.z] ^	   state2_tmp.w ;	
			state2.z = 	    state2_tmp.x  ^	     state2_tmp.y  ^ gal2[state2_tmp.z] ^ gal3[state2_tmp.w];	
			state2.w = gal3[state2_tmp.x] ^	     state2_tmp.y  ^	  state2_tmp.z  ^ gal2[state2_tmp.w];	
			
			state3.x = gal2[state3_tmp.x] ^ gal3[state3_tmp.y] ^	  state3_tmp.z  ^	   state3_tmp.w ;	
			state3.y =	    state3_tmp.x  ^ gal2[state3_tmp.y] ^ gal3[state3_tmp.z] ^	   state3_tmp.w ;	
			state3.z = 	    state3_tmp.x  ^	     state3_tmp.y  ^ gal2[state3_tmp.z] ^ gal3[state3_tmp.w];	
			state3.w = gal3[state3_tmp.x] ^	     state3_tmp.y  ^	  state3_tmp.z  ^ gal2[state3_tmp.w];	
			
			//state = AddRoundKey(state, keys[i]);
			state0 ^= keys[i*4];
			state1 ^= keys[i*4+1];
			state2 ^= keys[i*4+2];
			state3 ^= keys[i*4+3];
		}

		// state = SubBytes(state);
		// state = ShiftRows(state);
		state0_tmp.x = sbox[state0.x]; state0_tmp.y = sbox[state1.y]; state0_tmp.z = sbox[state2.z]; state0_tmp.w = sbox[state3.w]; 
		state1_tmp.x = sbox[state1.x]; state1_tmp.y = sbox[state2.y]; state1_tmp.z = sbox[state3.z]; state1_tmp.w = sbox[state0.w]; 
		state2_tmp.x = sbox[state2.x]; state2_tmp.y = sbox[state3.y]; state2_tmp.z = sbox[state0.z]; state2_tmp.w = sbox[state1.w]; 
		state3_tmp.x = sbox[state3.x]; state3_tmp.y = sbox[state0.y]; state3_tmp.z = sbox[state1.z]; state3_tmp.w = sbox[state2.w];
				
		// state = AddRoundKey(state, keys[rounds - 1]);
		// Save results to memory
		out[idx * 4    ] = state0_tmp ^ keys[(rounds - 1) * 4    ];
		out[idx * 4 + 1] = state1_tmp ^ keys[(rounds - 1) * 4 + 1];
		out[idx * 4 + 2] = state2_tmp ^ keys[(rounds - 1) * 4 + 2];
		out[idx * 4 + 3] = state3_tmp ^ keys[(rounds - 1) * 4 + 3];
	}
}

__kernel 
	__attribute__((reqd_work_group_size(256, 1, 1)))
	void AES_ECB_Decrypt3(
	__global uchar4* in, __global uchar4* out, 
	__global uchar4* keys, int rounds, int n)
{
	int idx   = get_global_id(0);
	int l_idx = get_local_id(0);
	
	uchar4 state0,     state1,     state2,     state3;
	uchar4 state0_tmp, state1_tmp, state2_tmp, state3_tmp;

	__local uchar inv_sbox[256];
	__local uchar gal9[256];
	__local uchar galB[256];
	__local uchar galD[256];
	__local uchar galE[256];

	// Cache frequently used data
	inv_sbox[l_idx] = AES_Inv_SBox[l_idx];
	gal9    [l_idx] = AES_Galois_9[l_idx];
	galB    [l_idx] = AES_Galois_B[l_idx];
	galD    [l_idx] = AES_Galois_D[l_idx];
	galE    [l_idx] = AES_Galois_E[l_idx];
	
	barrier(CLK_LOCAL_MEM_FENCE);
		
	if (idx <= n)
	{		
		//state = AddRoundKey(state, keys[rounds - 1]);
		state0_tmp = in[idx * 4]     ^ keys[(rounds - 1) * 4    ];
		state1_tmp = in[idx * 4 + 1] ^ keys[(rounds - 1) * 4 + 1];
		state2_tmp = in[idx * 4 + 2] ^ keys[(rounds - 1) * 4 + 2];
		state3_tmp = in[idx * 4 + 3] ^ keys[(rounds - 1) * 4 + 3];

		//state = InverseShiftRows(state);
		//state = InverseSubBytes(state);
		state0.x = inv_sbox[state0_tmp.x]; state0.y = inv_sbox[state3_tmp.y]; state0.z = inv_sbox[state2_tmp.z]; state0.w = inv_sbox[state1_tmp.w];
		state1.x = inv_sbox[state1_tmp.x]; state1.y = inv_sbox[state0_tmp.y]; state1.z = inv_sbox[state3_tmp.z]; state1.w = inv_sbox[state2_tmp.w]; 
		state2.x = inv_sbox[state2_tmp.x]; state2.y = inv_sbox[state1_tmp.y]; state2.z = inv_sbox[state0_tmp.z]; state2.w = inv_sbox[state3_tmp.w]; 
		state3.x = inv_sbox[state3_tmp.x]; state3.y = inv_sbox[state2_tmp.y]; state3.z = inv_sbox[state1_tmp.z]; state3.w = inv_sbox[state0_tmp.w];
		
		for (int i = rounds-2; i >= 1; i--)
		{	
			//state = AddRoundKey(state, keys[i]);
			state0 ^= keys[i * 4    ];
			state1 ^= keys[i * 4 + 1];
			state2 ^= keys[i * 4 + 2];
			state3 ^= keys[i * 4 + 3];

			//state = InverseMixColumns(state);
			state0_tmp.x = galE[state0.x] ^ galB[state0.y] ^ galD[state0.z] ^ gal9[state0.w];
			state0_tmp.y = gal9[state0.x] ^ galE[state0.y] ^ galB[state0.z] ^ galD[state0.w];
			state0_tmp.z = galD[state0.x] ^ gal9[state0.y] ^ galE[state0.z] ^ galB[state0.w];
			state0_tmp.w = galB[state0.x] ^ galD[state0.y] ^ gal9[state0.z] ^ galE[state0.w];

			state1_tmp.x = galE[state1.x] ^ galB[state1.y] ^ galD[state1.z] ^ gal9[state1.w];
			state1_tmp.y = gal9[state1.x] ^ galE[state1.y] ^ galB[state1.z] ^ galD[state1.w];
			state1_tmp.z = galD[state1.x] ^ gal9[state1.y] ^ galE[state1.z] ^ galB[state1.w];
			state1_tmp.w = galB[state1.x] ^ galD[state1.y] ^ gal9[state1.z] ^ galE[state1.w];

			state2_tmp.x = galE[state2.x] ^ galB[state2.y] ^ galD[state2.z] ^ gal9[state2.w];
			state2_tmp.y = gal9[state2.x] ^ galE[state2.y] ^ galB[state2.z] ^ galD[state2.w];
			state2_tmp.z = galD[state2.x] ^ gal9[state2.y] ^ galE[state2.z] ^ galB[state2.w];
			state2_tmp.w = galB[state2.x] ^ galD[state2.y] ^ gal9[state2.z] ^ galE[state2.w];

			state3_tmp.x = galE[state3.x] ^ galB[state3.y] ^ galD[state3.z] ^ gal9[state3.w];
			state3_tmp.y = gal9[state3.x] ^ galE[state3.y] ^ galB[state3.z] ^ galD[state3.w];
			state3_tmp.z = galD[state3.x] ^ gal9[state3.y] ^ galE[state3.z] ^ galB[state3.w];
			state3_tmp.w = galB[state3.x] ^ galD[state3.y] ^ gal9[state3.z] ^ galE[state3.w];

			//state = InverseShiftRows(state);
			//state = InverseSubBytes(state);
			state0.x = inv_sbox[state0_tmp.x]; state0.y = inv_sbox[state3_tmp.y]; state0.z = inv_sbox[state2_tmp.z]; state0.w = inv_sbox[state1_tmp.w];
			state1.x = inv_sbox[state1_tmp.x]; state1.y = inv_sbox[state0_tmp.y]; state1.z = inv_sbox[state3_tmp.z]; state1.w = inv_sbox[state2_tmp.w]; 
			state2.x = inv_sbox[state2_tmp.x]; state2.y = inv_sbox[state1_tmp.y]; state2.z = inv_sbox[state0_tmp.z]; state2.w = inv_sbox[state3_tmp.w]; 
			state3.x = inv_sbox[state3_tmp.x]; state3.y = inv_sbox[state2_tmp.y]; state3.z = inv_sbox[state1_tmp.z]; state3.w = inv_sbox[state0_tmp.w];
		}	
		
		out[idx * 4    ] = state0 ^ keys[0];
		out[idx * 4 + 1] = state1 ^ keys[1];
		out[idx * 4 + 2] = state2 ^ keys[2];
		out[idx * 4 + 3] = state3 ^ keys[3];
	}
}



// AES CTR encrpypt
// * Each kernel computes it's own block
// * Data should be pre-paded
// * Used the data type int4 and removed fucntions
// * Local memory for tables
__kernel 
	__attribute__((reqd_work_group_size(256, 1, 1)))
	void AES_CTR_Encrypt(
	__global uchar4* in, __global uchar4* out, 
	__global uchar4* keys, int rounds, int n, 
	__global uchar4* nonce)
{
	int idx   = get_global_id(0);
	int l_idx = get_local_id(0);
	
	uchar4 state0,     state1,     state2,     state3;
	uchar4 state0_tmp, state1_tmp, state2_tmp, state3_tmp;

	__local uchar sbox[256];
	__local uchar gal2[256];
	__local uchar gal3[256];

	// Cache frequently used data
	sbox[l_idx] = AES_SBox    [l_idx];
	gal2[l_idx] = AES_Galois_2[l_idx];
	gal3[l_idx] = AES_Galois_3[l_idx];
	
	barrier(CLK_LOCAL_MEM_FENCE);
		
	if (idx <= n)
	{
		// Read nonce from memory
		//state = AddRoundKey(state, keys[0]);
		state0 = nonce[0] ^ keys[0];
		state1 = nonce[1] ^ keys[1];
		state2 = nonce[2] ^ keys[2];
		state3 = *((uchar4 *)(&idx)) ^ keys[3];
			
		for (int i = 1; i < rounds-1; i++)
		{
			// state = SubBytes(state);
			// state = ShiftRows(state);
			state0_tmp.x = sbox[state0.x]; state0_tmp.y = sbox[state1.y]; state0_tmp.z = sbox[state2.z]; state0_tmp.w = sbox[state3.w]; 
			state1_tmp.x = sbox[state1.x]; state1_tmp.y = sbox[state2.y]; state1_tmp.z = sbox[state3.z]; state1_tmp.w = sbox[state0.w]; 
			state2_tmp.x = sbox[state2.x]; state2_tmp.y = sbox[state3.y]; state2_tmp.z = sbox[state0.z]; state2_tmp.w = sbox[state1.w]; 
			state3_tmp.x = sbox[state3.x]; state3_tmp.y = sbox[state0.y]; state3_tmp.z = sbox[state1.z]; state3_tmp.w = sbox[state2.w];

			// state = MixColumns(state);
			state0.x = gal2[state0_tmp.x] ^ gal3[state0_tmp.y] ^	  state0_tmp.z  ^      state0_tmp.w ;	
			state0.y =	    state0_tmp.x  ^ gal2[state0_tmp.y] ^ gal3[state0_tmp.z] ^      state0_tmp.w ;
			state0.z = 	    state0_tmp.x  ^      state0_tmp.y  ^ gal2[state0_tmp.z] ^ gal3[state0_tmp.w];	
			state0.w = gal3[state0_tmp.x] ^	     state0_tmp.y  ^	  state0_tmp.z  ^ gal2[state0_tmp.w];	
			
			state1.x = gal2[state1_tmp.x] ^ gal3[state1_tmp.y] ^	  state1_tmp.z  ^      state1_tmp.w ;	
			state1.y =	    state1_tmp.x  ^ gal2[state1_tmp.y] ^ gal3[state1_tmp.z] ^	   state1_tmp.w ;	
			state1.z = 	    state1_tmp.x  ^	     state1_tmp.y  ^ gal2[state1_tmp.z] ^ gal3[state1_tmp.w];	
			state1.w = gal3[state1_tmp.x] ^	     state1_tmp.y  ^	  state1_tmp.z  ^ gal2[state1_tmp.w];	
			
			state2.x = gal2[state2_tmp.x] ^ gal3[state2_tmp.y] ^	  state2_tmp.z  ^      state2_tmp.w ;	
			state2.y =	    state2_tmp.x  ^ gal2[state2_tmp.y] ^ gal3[state2_tmp.z] ^	   state2_tmp.w ;	
			state2.z = 	    state2_tmp.x  ^	     state2_tmp.y  ^ gal2[state2_tmp.z] ^ gal3[state2_tmp.w];	
			state2.w = gal3[state2_tmp.x] ^	     state2_tmp.y  ^	  state2_tmp.z  ^ gal2[state2_tmp.w];	
			
			state3.x = gal2[state3_tmp.x] ^ gal3[state3_tmp.y] ^	  state3_tmp.z  ^	   state3_tmp.w ;	
			state3.y =	    state3_tmp.x  ^ gal2[state3_tmp.y] ^ gal3[state3_tmp.z] ^	   state3_tmp.w ;	
			state3.z = 	    state3_tmp.x  ^	     state3_tmp.y  ^ gal2[state3_tmp.z] ^ gal3[state3_tmp.w];	
			state3.w = gal3[state3_tmp.x] ^	     state3_tmp.y  ^	  state3_tmp.z  ^ gal2[state3_tmp.w];	
			
			//state = AddRoundKey(state, keys[i]);
			state0 ^= keys[i*4];
			state1 ^= keys[i*4+1];
			state2 ^= keys[i*4+2];
			state3 ^= keys[i*4+3];
		}

		// state = SubBytes(state);
		// state = ShiftRows(state);
		state0_tmp.x = sbox[state0.x]; state0_tmp.y = sbox[state1.y]; state0_tmp.z = sbox[state2.z]; state0_tmp.w = sbox[state3.w]; 
		state1_tmp.x = sbox[state1.x]; state1_tmp.y = sbox[state2.y]; state1_tmp.z = sbox[state3.z]; state1_tmp.w = sbox[state0.w]; 
		state2_tmp.x = sbox[state2.x]; state2_tmp.y = sbox[state3.y]; state2_tmp.z = sbox[state0.z]; state2_tmp.w = sbox[state1.w]; 
		state3_tmp.x = sbox[state3.x]; state3_tmp.y = sbox[state0.y]; state3_tmp.z = sbox[state1.z]; state3_tmp.w = sbox[state2.w];
				
		// state = AddRoundKey(state, keys[rounds - 1]);
		// Save results to memory
		out[idx * 4    ] = in[idx * 4    ] ^ state0_tmp ^ keys[(rounds - 1) * 4    ];
		out[idx * 4 + 1] = in[idx * 4 + 1] ^ state1_tmp ^ keys[(rounds - 1) * 4 + 1];
		out[idx * 4 + 2] = in[idx * 4 + 2] ^ state2_tmp ^ keys[(rounds - 1) * 4 + 2];
		out[idx * 4 + 3] = in[idx * 4 + 3] ^ state3_tmp ^ keys[(rounds - 1) * 4 + 3];
	}
}